PREFIX :      <http://www.cattid.uniroma1.it/2012/pampers#>
PREFIX xml:   <http://www.swows.org/xml#>
PREFIX doc:   <http://www.swows.org/xml/instance#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX geo:   <http://www.fao.org/countryprofiles/geoinfo/geopolitical/resource/>

CONSTRUCT {

  ?country
    a :Country ;
    geo:codeISO2 ?codeISO2upper ;
    :option ?option .
  ?option
    :id ?optionId ;
    :value ?proportion .

}
WHERE {

  ?country
    a :Country ;
    geo:codeISO2 ?codeISO2upper ;
    :option ?option .
  ?option
    :id ?optionId ;
    :value ?value .
  {
    SELECT ?optionId (MAX(?value) AS ?maxValue)
    WHERE {
      ?country
        a :Country ;
        :option ?option .
      ?option
        :id ?optionId ;
        :value ?value .
    } GROUP BY ?optionId
  } .
  {
    SELECT ?optionId (MIN(?value) AS ?minValue)
    WHERE {
      ?country
        a :Country ;
        :option ?option .
      ?option
        :id ?optionId ;
        :value ?value .
    } GROUP BY ?optionId
  } .
  BIND( ( ?value - ?minValue ) / ( ?maxValue - ?minValue ) AS ?proportion ).

}

