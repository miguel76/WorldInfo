PREFIX :      <http://www.cattid.uniroma1.it/2012/pampers#>
PREFIX xml:   <http://www.swows.org/xml#>
PREFIX doc:   <http://www.swows.org/xml/instance#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX geo:   <http://www.fao.org/countryprofiles/geoinfo/geopolitical/resource/>
PREFIX nn:    <http://www.swows.org/xml/no-namespace#>
PREFIX svg:   <http://www.w3.org/2000/svg#>
PREFIX xlink: <http://www.w3.org/1999/xlink#>
PREFIX local: <file:///home/miguel/git/WorldInfo/svg/>

CONSTRUCT {

  ?selectedOption_g xml:hasChild <#selectedOption_use> .
  <#selectedOption_use>  a svg:use ; xlink:href "#radioPoint".
  ?unselectedOption_radioBorder
    nn:cursor "pointer" ;
    xml:listenedEventType "mousedown" .

  ?country_elementOrDescendant nn:style ?country_style_value .

}
WHERE {

  GRAPH :selectedOption {
    swi:GraphRoot :selectedOption ?selectedOption .
  } .
  GRAPH :config {
    swi:GraphRoot :possibleOption/:id ?unselectedOption .
  } .
  FILTER( ?selectedOption != ?unselectedOption ) .
  BIND( URI( CONCAT( STR(local:BlankMapWithRadioBox.svg), "#option_", ?selectedOption ) ) AS ?selectedOption_g ).
  BIND( URI( CONCAT( STR(local:BlankMapWithRadioBox.svg), "#option_", ?unselectedOption, "_radioBorder" ) ) AS ?unselectedOption_radioBorder ).

  ?country
    a :Country ;
    geo:codeISO2 ?codeISO2upper ;
    :option ?option .
  ?option
    :id ?selectedOption ;
    :value ?proportion .
  BIND( URI( CONCAT( STR( local:BlankMapWithRadioBox.svg ), "#", LCASE(?codeISO2upper) ) ) AS ?country_element ) .
  GRAPH :descendants {
    ?country_element :descendantOrSelf ?country_elementOrDescendant .
  } .
  BIND( STR(255) AS ?red ).
  BIND( STR(xsd:integer( ROUND( xsd:decimal( (1 - ?proportion) * 255) ) ) ) AS ?green ).
  BIND( STR(xsd:integer( ROUND( xsd:decimal( (1 - ?proportion) * 255) ) ) ) AS ?blue ).
  BIND( CONCAT( "fill: rgb(", ?red, ",", ?green, ",", ?blue, ")" ) AS ?country_style_value ) .

}

