PREFIX :      <http://www.cattid.uniroma1.it/2012/pampers#>
PREFIX xml:   <http://www.swows.org/xml#>
PREFIX doc:   <http://www.swows.org/xml/instance#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX geo:   <http://www.fao.org/countryprofiles/geoinfo/geopolitical/resource/>

CONSTRUCT {

  ?selectedOption_g xml:hasChild <#selectedOption_use> .
  <#selectedOption_use>
    a xml:Element ;
    xml:namespace <http://www.w3.org/2000/svg> ;
    xml:nodeName "use"^^xsd:string ;
    xml:hasAttribute <#selectedOption_href> .
  <#selectedOption_href>
    a xml:Attr ;
    xml:nodeName "href"^^xsd:string ;
    xml:namespace <http://www.w3.org/1999/xlink> ;
    xml:nodeValue "#radioPoint"^^xsd:string .

  ?unselectedOption_radioBorder xml:listenedEventType "mousedown" .

  ?country_elementOrDescendant xml:hasAttribute ?country_style .
  ?country_style
    a xml:Attr ;
    xml:nodeName "style"^^xsd:string ;
    xml:nodeValue ?country_style_value .

}
WHERE {

  GRAPH :selectedOption {
    swi:GraphRoot :selectedOption ?selectedOption .
  } .
  GRAPH :config {
    swi:GraphRoot :possibleOption ?unselectedOption .
  } .
  FILTER( ?selectedOption != ?unselectedOption ) .
  BIND( URI( CONCAT( STR(doc:option_), ?selectedOption ) ) AS ?selectedOption_g ).
  BIND( URI( CONCAT( STR(doc:option_), ?unselectedOption, "_radioBorder" ) ) AS ?unselectedOption_radioBorder ).

  ?country
    a :Country ;
    geo:codeISO2 ?codeISO2upper ;
    :value ?proportion .
  BIND( URI( CONCAT( STR( doc: ), LCASE(?codeISO2upper) ) ) AS ?country_element ) .
  GRAPH :descendants {
    ?country_element :descendantOrSelf ?country_elementOrDescendant .
  } .
  BIND( STR(255) AS ?red ).
  BIND( STR(xsd:integer( ROUND( xsd:decimal( (1 - ?proportion) * 255) ) ) ) AS ?green ).
  BIND( STR(xsd:integer( ROUND( xsd:decimal( (1 - ?proportion) * 255) ) ) ) AS ?blue ).
  BIND( CONCAT( "fill: rgb(", ?red, ",", ?green, ",", ?blue, ")" ) AS ?country_style_value ) .
  BIND( URI( CONCAT( STR( :country_style_ ), MD5( STR(?country) ) ) ) AS ?country_style ) .

}

