PREFIX :      <http://www.swows.org/samples/WorldInfo#>
PREFIX xml:   <http://www.swows.org/xml#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX geo:   <http://www.fao.org/countryprofiles/geoinfo/geopolitical/resource/>

CONSTRUCT {
  ?country
    a :Country ;
    geo:codeISO2 ?codeISO2upper ;
    :option ?optionGdp, ?optionDensity, ?optionAgriAreaRate .
  ?optionGdp
    :id "gdp"^^xsd:string ;
    :value ?gdpPerCapita .
  ?optionDensity
    :id "density"^^xsd:string ;
    :value ?density .
  ?optionAgriAreaRate
    :id "agriAreaRate"^^xsd:string ;
    :value ?agriAreaRate .
}
WHERE {
  ?country
    a geo:self_governing ;
    geo:codeISO2 ?codeISO2upper ;
    geo:GDPTotalInCurrentPrices ?gdp ;
    geo:populationTotal ?pop ;
    geo:landAreaTotal ?landArea ;
    geo:agriculturalAreaTotal ?agriArea .
  BIND( ?gdp / ?pop AS ?gdpPerCapita ) .
  BIND( ?pop / ?landArea AS ?density ) .
  BIND( ?agriArea / ?landArea AS ?agriAreaRate ) .
  BIND( URI( CONCAT( STR(:optionGdp_), MD5(STR(?country)) ) ) AS ?optionGdp ) .
  BIND( URI( CONCAT( STR(:optionDensity_), MD5(STR(?country)) ) ) AS ?optionDensity ) .
  BIND( URI( CONCAT( STR(:optionAgriAreaRate_), MD5(STR(?country)) ) ) AS ?optionAgriAreaRate ) .
}

